/*
 * Op script to send series of pings to remote-host, each one increasing in
 * size, may be useful for troubleshooting MTU/MSS issues.
 *
 * Size of the packet starts from $min-size, increases by $step and
 * ends at $max-size
 */
 
version 1.1;
 
ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
 
import "../import/junos.xsl";
 
var $arguments = {
    <argument> {
        <name> "routing-instance";
        <description> "Routing instance name";
    }
    <argument> {
        <name> "remote-host";
        <description> "Host name or ip-address to ping";
    }
    <argument> {
        <name> "min-size";
        <description> "Minimum packet size";
    }
    <argument> {
        <name> "max-size";
        <description> "Maximum packet size";
    }
    <argument> {
        <name> "step";                  
        <description> "Packet size difference";
    }
}
 
param $routing-instance;
param $remote-host;
param $min-size = '1400';
param $max-size = '1600';
param $step = '100';
 
match / {
 
    call ping($routing-instance, $remote-host, $cur-size = $min-size, $max-size, $step);
 
}
 
/*
 * Ping the remote-host with different packet size and display the result
 */
template ping ($routing-instance, $remote-host, $cur-size, $max-size, $step) {
 
    if ($cur-size <= $max-size) {
        var $ping-rpc = {
            <ping> {
                if (boolean($routing-instance)) {
                    <routing-instance> $routing-instance;
                }
                <host> $remote-host;
                <size> $cur-size;
                <count> 1;
                <do-not-fragment>;
            }
        }
 
        expr jcs:output("Executing command: ping ", $routing-instance, " ", $remote-host, " size ",
                         $cur-size, " count 1 do-not-fragment");
 
        /*
         * Invoking ping RPC and getting the result into $ping-out
         */
        var $ping-out = jcs:invoke($ping-rpc);
 
        if ($ping-out/ping-success) {
            expr jcs:output("Ping Success !");
        } else {
            expr jcs:output("Ping Failed .");
        }
 
        call ping($routing-instance, $remote-host, $cur-size = $cur-size + $step, $max-size,
                  $step);
    }
}